plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.node-gradle.node" version "2.2.4"
}

repositories {
    jcenter()
}

import groovy.json.JsonSlurper
def packageJson = new JsonSlurper().parse file('package.json')
version = packageJson.version

dependencies {
    compile 'org.msgpack:msgpack-core:0.8.20'
    testImplementation 'org.graalvm.js:js:20.1.0'
    testImplementation 'net.jqwik:jqwik:1.3.2'
    testImplementation 'org.assertj:assertj-core:3.16.1'
}

ext.rollupOutputDir = file("$buildDir/js")

task npmRollup(type: NpmTask) {
    args = ["run", "rollup"]
}

sourceSets.test.output.dir rollupOutputDir, builtBy: npmRollup

test {
    useJUnitPlatform {
        includeEngines 'jqwik'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/polypoly-eu/bubblewrap")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId = 'eu.polypoly'
            artifactId = 'bubblewrap'

            from(components.java)
        }
    }
}
